/**
 * Pivonia Library
 *
 */
plugins {
    id "java"
    id "groovy" // for Spock
    id "maven"
    id "maven-publish"
}

configurations {
    deployerJars
}

/**
 * Settings
 */
rootProject.group = "eu.phisikus.pivonia"
rootProject.version = "0.0.16"


/**
 * Dependencies
 */

repositories {
    mavenCentral()
}

/**
 * Java
 */
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}


dependencies {

    /**
     * Lombok
     */
    annotationProcessor "org.projectlombok:lombok:1.18.12"
    implementation "org.projectlombok:lombok:1.18.12"

    /**
     * Dependency Injection
     */
    annotationProcessor "com.google.dagger:dagger-compiler:2.27"
    implementation "com.google.dagger:dagger:2.27"

    /**
     * Serialization
     */
    compile "de.undercouch:bson4jackson:2.9.2"

    /**
     * Encryption
     */
    compile "com.google.crypto.tink:tink:1.3.0"

    /**
     * Utils
     */
    compile "io.vavr:vavr:0.10.2"
    compile "io.github.resilience4j:resilience4j-retry:1.3.1"
    compile "org.apache.logging.log4j:log4j-api:2.13.1"
    compile "org.apache.logging.log4j:log4j-core:2.13.1"
    compile "io.reactivex.rxjava2:rxjava:2.2.19"

    /**
     * Testing
     */
    testCompile "org.codehaus.groovy:groovy-all:2.5.8"
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
    testCompile "cglib:cglib-nodep:3.3.0"
    testCompile "org.objenesis:objenesis:3.1"
    testCompile "org.awaitility:awaitility:4.0.1"


    /**
     * Publishing
     */
    deployerJars "org.apache.maven.wagon:wagon-ssh:3.3.4"
}


/**
 * Test configuration
 */
test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

/**
 * Maven Publishing
 */
publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version

            from components.java

            pom {
                name = "Pivonia"
                description = "Message Passing Library"
            }
        }
    }
}

uploadArchives {
    if (project.hasProperty("deploymentUrl")) {
        final deploymentUrl = project.property("deploymentUrl")
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: deploymentUrl)
        }
    }
}

def getGitHash = {
    final stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


/**
 * Custom task for CI
 */
tasks.register("release") {
    description = "Releases new library version"
    dependsOn = ["clean", "build", "uploadArchives"]
}

tasks.register("releaseSnapshot") {
    description = "Releases snapshot version"
    rootProject.version = rootProject.version + "-" + getGitHash()
    dependsOn = ["clean", "build", "uploadArchives"]
}
